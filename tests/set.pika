%generate member
%generate insert

-- Sets as BSTs

Sll : @(s : set) layout[-x](List);
Sll Nil := s == {} ;; emp;
Sll (Cons head tail) :=
  exists +nxt.
  s == ({head} ++ s1)
    ;;
  x :-> head **
  (x+1) :-> nxt **
  Sll tail @s1 [nxt];

SetLayout : @(s : set) layout[-x](Set);
SetLayout Empty := s == {} ;; emp;
SetLayout (Node left v right) :=
  exists +p, +q.
  s == ({v} ++ s1 ++ s2)
    ;;
  x :-> p **
  (x+1) :-> v **
  (x+2) :-> q **
  SetLayout left @s1 [p] **
  SetLayout right @s2 [q];

synth setToSll : SetLayout @s -> Sll @s;

member : Int -> SetLayout -> Bool;
member n Empty := False;
member n (Node l a r) | n < a := member n l;
member n (Node l a r) | a < n := member n r;
member n (Node l a r) | ((not (n < a)) && (not (a < n))) := True;

insert : Int -> SetLayout -> SetLayout;
insert n Empty := Node (Empty [SetLayout]) n (Empty [SetLayout]) [SetLayout];
insert n (Node l a r) | n < a := Node (insert n l) a r [SetLayout];
insert n (Node l a r) | a < n := Node l a (insert n r) [SetLayout];
insert n (Node l a r) | ((not (n < a)) && (not (a < n))) := Node l a r [SetLayout];

%test "insert1" SetLayout:
  insert 3
    (insert 2
      (insert 5
        (insert 20
          (Empty [SetLayout]))));

%test "member1" Bool:
  member 20
    (insert 3
      (insert 2
        (insert 5
          (insert 20
            (Empty [SetLayout])))));

%test "member2" Bool:
  member 100
    (insert 3
      (insert 2
        (insert 5
          (insert 20
            (Empty [SetLayout])))));

